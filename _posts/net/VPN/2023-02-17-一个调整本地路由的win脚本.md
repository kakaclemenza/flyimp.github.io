---
layout: post
title: 一个调整本地路由的win脚本
category: net
typora-root-url: ../../..
---

VPN需要开启全局代理，前提条件：

1. 代理服允许放通全局流量
2. 客户端允许放通全服流量
3. 调整客户端路由，主要包括：
   * 加一条路由，将VPN服路由以”x.x.x.x/32“方式从旧默认网关路由中独立出来，这样修改默认路由后到达VPN服的路由能正常走通。
   * 加一条新默认网关路由，将"0.0.0.0/0"设置为使用VPN服overlay IP作为网关
   * 将旧默认网关路由删除，此时上面配置的新默认网关路由开始生效

这里记录一个win平台调整网关的脚本，此bat脚本也可以用作设置win下复杂网络配置的参考：

```bat
@echo off
set overseaGw=26.1.0.1
set origGw=
set ethIdx=
set ethName=
set tunIdx=
set tunName=
set restoreNet=121.201.64.0/24
set restoreGw=
set restoreIdx=
set restoreName=
for /f "tokens=4,5,6" %%i in ('netsh interface ip show route') do (
	rem if判定字符串为空，使用`"%xxx%"==""`
	if "%ethIdx%"=="" if "%%i"=="0.0.0.0/0" (
		set ethIdx=%%j
		set origGw=%%k
	)
	if "%restoreIdx%"=="" if "%%i"=="%restoreNet%" (
		set restoreIdx=%%j
		set restoreGw=%%k
	)
	if "%tunIdx%"=="" if "%%k"=="%overseaGw%" (
		set tunIdx=%%j
	)
)
rem for进行分割，对第4段后的内容，使用`%%k`来获取，防止网卡名带空格
for /F "tokens=1,4*" %%i IN ('netsh interface ip show interfaces') do (
	if "%tunName%"=="" if "%%i"=="%tunIdx%" (
		set tunName=%%k
	)
	if "%ethName%"=="" if "%%i"=="%ethIdx%" (
		set ethName=%%k
	)
	if "%%i"=="%restoreIdx%" (
		set restoreName=%%k
	)
)

echo origGw: %origGw%
echo ethIdx: %ethIdx%
echo ethName: %ethName%
echo tunIdx: %tunIdx%
echo tunName: %tunName%
echo restoreIdx: %restoreIdx%
echo restoreName: %restoreName%
echo restoreGw: %restoreGw%

if "%tunIdx%"=="" (
	echo [ERR ] No overseaGw find, please run vpncli for oversea first!!!
	goto End
)

if "%origGw%"=="%overseaGw%" (
	echo [WARN] Already use global route to overseaGw find, do restore!!!
	echo [INFO] start to restore global route.
	rem 添加代理服走旧的网关
	netsh interface ip del route 121.201.64.0/24 "%restoreName%" %restoreGw%
	rem 设置新的默认网关为VPN服
	netsh interface ip del route 0.0.0.0/0 "%tunName%" %overseaGw%
	rem 删除旧的默认网关
	netsh interface ip add route 0.0.0.0/0 "%restoreName%" %restoreGw% store=active
) else (
	echo [INFO] start to set global route.
	rem 添加代理服走旧的网关
	netsh interface ip add route 121.201.64.0/24 "%ethName%" %origGw% store=active
	rem 设置新的默认网关为VPN服
	netsh interface ip add route 0.0.0.0/0 "%tunName%" %overseaGw% store=active
	rem 删除旧的默认网关
	netsh interface ip del route 0.0.0.0/0 "%ethName%" %origGw%
)
echo [INFO] finished

netsh interface ip show route

:END
pause
```

